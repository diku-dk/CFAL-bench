use Array: all;
use Math: all;
use StdIO: all;
use Benchmarking: all;

#define REAL float
#define TO_REAL tof

/**
 * Initializes VarX as outer product of VarX1 and VarX2.
 */
REAL[NUM_Y], REAL[NUM_X] updateParams(REAL[NUM_X] X, REAL[NUM_Y] Y, REAL t)
{
  VarX1 = {[j] -> exp(2.0f * Y[j] - NU * NU * t)};
  VarX2 = {[i] -> pow(X[i], 2.0f * BETA)};

  return (VarX1, VarX2);
}

/**
 * Initializes indX, indY, X, Y, Dxx, Dyy
 */
inline
int, int, REAL[NUM_X], REAL[NUM_Y], REAL[3], REAL[3] initGrid()
{
  stdX = 20.0f * ALPHA * S0 * sqrt(T);
  dx = stdX / TO_REAL(NUM_X);
  indX = toi(S0 / dx);
  X = {[i] -> TO_REAL(i) * dx - TO_REAL(indX) * dx + S0 | [i] < [NUM_X]};
  Dxx = [1.0f / (dx * dx), -2.0f / (dx * dx), 1.0f / (dx * dx)];

  stdY = 10.0f * NU * sqrt(T);
  dy = stdY / TO_REAL(NUM_Y);
  indY = NUM_Y / 2;
  Dyy = [1.0f / (dy * dy), -2.0f / (dy * dy), 1.0f / (dy * dy)];

  Y = {[i] -> TO_REAL(i) * dy - TO_REAL(indY) * dy + log(ALPHA)
            | [i] < [NUM_Y]};

  return (indX, indY, X, Y, Dxx, Dyy);
}

inline 
REAL[.] tridag(REAL[.] a, REAL[.] b, REAL[.] c, REAL[.] y)
{
  n = shape(y)[0];

  /* This is the modified Thomas method from Numerical Methods.
   * Note that the non-zeroes in a row are a, b, c in this application,
   * and b, a, c in Numerical Methods.
   * We store gamma in b. */
  b[0] = 1.0f / b[0];
  y[0] = b[0] * y[0];

  for (i = 1; i < n; i++) {
      b[i] = 1.0f / (b[i] - a[i] * b[i - 1] * c[i - 1]);
      y[i] = b[i] * (y[i] - a[i] * y[i - 1]);
  }

  for (i = n - 2; i >= 1; i--) {
      y[i] = y[i] - b[i] * c[i] * y[i + 1];
  }

  return y;
}

inline
REAL[NUM_X] implicit_x(REAL VarX1, REAL[NUM_X] VarX2, REAL[3] Dxx,
                   REAL dtInv, REAL[NUM_X] uu)
{
  a = {[j] -> -0.25f * VarX1 * VarX2[j] * Dxx[0] | [1] <= [j] < [NUM_X - 1];
       [j] -> 0f                                | [NUM_X - 1] <= [j] < [NUM_X]}; 

  return tridag(a, dtInv - 2f * a, a, uu); 
}

inline
REAL[NUM_Y] implicit_y(REAL VarY, REAL[3] Dyy, REAL dtInv,
                       REAL[NUM_Y] yy)
{
  a = {[j] -> -0.25f * VarY * Dyy[0] | [1] <= [j] < [NUM_Y - 1];
       [j] -> 0f                     | [NUM_Y - 1] <= [j] < [NUM_Y]}; 

  return tridag(a, dtInv - 2f * a, a, yy);
}

inline
REAL[NUM_X, NUM_Y] rollback(REAL dtInv, REAL[3] Dxx,
                            REAL[NUM_X] X, REAL[NUM_Y] Y, int t,
                            REAL[3] Dyy, REAL VarY,
                            REAL[NUM_X, NUM_Y] ResultE)
{
  VarX1, VarX2 = updateParams(X, Y, T * TO_REAL(t) / TO_REAL(NUM_T - 1));

  /* explicit y */
  V = {[i, j] -> 0.5f * sum({[k] -> ResultE[i, j - 1 + k] * VarY * Dyy[k]
                                  | [0] <= [k] < [3]})
               | [0, 1] <= [i, j] < [NUM_X, NUM_Y - 1];
       [i, j] -> 0f
               | [0, NUM_Y - 1] <= [i, j] < [NUM_X, NUM_Y]
      };

  /* explicit x */
  U = {[j, i] -> dtInv * ResultE[i, j]
               | [0, 0] <= [j, i] < [NUM_Y, 1];
       [j, i] -> dtInv * ResultE[i, j] + 0.25f *
                  sum({[k] -> ResultE[i - 1 + k, j] *
                              VarX1[j] * VarX2[i] * Dxx[k]
                            | [0] <= [k] < [3]})
               | [0, 1] <= [j, i] < [NUM_Y, NUM_X - 1];
       [j, i] -> dtInv * ResultE[i, j]
               | [0, NUM_X - 1] <= [j, i] < [NUM_Y, NUM_X]
       };

  U = {[j, i] -> U[j, i] + V[i, j]};

  /* implicit x */
  U = {[j] -> implicit_x(VarX1[j], VarX2, Dxx, dtInv, U[j])};

  /* implicit y */
  ResultE = {[i] -> implicit_y(VarY, Dyy, dtInv,
                      {[j] -> dtInv * U[j, i] - V[i, j] / 2.0f})};

  return ResultE;
}

inline
REAL value(REAL strike, int indX, int indY,
           REAL[NUM_X] X, REAL[NUM_Y] Y,
           REAL[3] Dxx, REAL[3] Dyy)
{
  ResultE = {[i, j] -> max(X[i] - strike, 0.0f) | [i, j] < [NUM_X, NUM_Y]};

  for (t = NUM_T - 2; t >= 0; t--) {
    ResultE = rollback(TO_REAL(NUM_T - 1) / T, Dxx, X, Y, t, 
                       Dyy, NU * NU, ResultE);
  }

  return ResultE[indX, indY];
}

int main()
{
  fprintf(stdout, "\n// SaC Volatility Calibration Benchmark:\n");

  itime = getInterval("time", 1);
  start(itime);
  indX, indY, X, Y, Dxx, Dyy = initGrid();

  result = {[i] -> value(TO_REAL(i) / 1000.0f, indX, indY, X, Y, Dxx, Dyy)
                 | [i] < [OUTER]};
  end(itime);
  print(result);

  time, unit = returnResultUnit(itime);
  printf("This took %f%s.\n", time, unit);

  return 0;
}
